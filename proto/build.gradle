plugins {
    id "java"
    id "com.google.protobuf" version "0.8.12"
}

dependencies {
    api(group: "com.google.protobuf", name: "protobuf-java", version: Versions.protobufJava)
    api(group: "io.grpc", name: "grpc-all", version: Versions.grpcAll)
    api(group: "javax.annotation", name: "javax.annotation-api", version: Versions.javaxAnnotationAPI)
}

sourceSets {
    main {
        proto {
            srcDir("$projectDir/bowler-proto/src/proto")
        }
        java {
            srcDir("$buildDir/generated/source/proto/main/grpc")
            srcDir("$buildDir/generated/source/proto/main/java")
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${Versions.protobufJava}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${Versions.grpcAll}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            // Disable caching because of https://github.com/google/protobuf-gradle-plugin/issues/180
            task.outputs.upToDateWhen { false }
            task.plugins {
                grpc { }
            }
            project.tasks.named("classes").configure {
                it.dependsOn(task)
//                it.outputs.upToDateWhen { false }
            }
        }
    }
}
